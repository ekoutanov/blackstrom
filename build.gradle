plugins {
  id 'com.jfrog.bintray' version '1.7.3'
}
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.obsidiandynamics.blackstrom'
version = '0.1.0-SNAPSHOT'
  
def envUser = 'BINTRAY_USER'
def envKey = 'BINTRAY_KEY'
task bintrayCredentialsCheck {
  doLast {
    if (System.getenv(envUser) == null) {
      throw new GradleException("No Bintray username specified; set with 'export ${envUser}=<username>'")
    }
    if (System.getenv(envKey) == null) {
      throw new GradleException("No Bintray key specified; set with 'export ${envKey}=<key>'")
    }
  }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.bintray'

  repositories {
    jcenter()
    maven {
      url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
  }
  
  dependencies {
    testCompile 'com.obsidiandynamics.indigo:indigo-assurance:1.1.0'
    testCompile 'com.obsidiandynamics.fulcrum:fulcrum-assert:0.4.0'
    testCompile 'com.obsidiandynamics.fulcrum:fulcrum-await:0.4.0'
    testCompile 'com.obsidiandynamics.fulcrum:fulcrum-junit:0.4.0'
    testCompile 'com.obsidiandynamics.yconf:yconf-snakeyaml:0.2.1'
    testCompile 'org.mockito:mockito-core:2.9.0'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    testRuntime 'org.slf4j:slf4j-log4j12:1.7.25'
    testRuntime 'log4j:log4j:1.2.17'
  }

  test {
    exclude '**/*IT.class'
  }
  
  task integrationTest(type: Test, description: 'Runs integration tests.', group: 'Verification') {
    include '**/*IT.class'
  }

  jacoco {
    toolVersion = '0.8.1-SNAPSHOT'
  }

  jacocoTestReport {
    additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories = files(sourceSets.main.output)
    reports {
      html.enabled = true
      xml.enabled = true
      csv.enabled = false
    }
  }

  // invoke this task when ready to publish to Bintray
  bintrayUpload {
    dependsOn ':bintrayCredentialsCheck'
  }
}

subprojects {
  dependencies {
    testCompile project(':').sourceSets.test.output
  }
}

task jacocoRootReport(type: JacocoReport) {
  dependsOn = allprojects.test
  additionalSourceDirs = files(allprojects.sourceSets.main.allSource.srcDirs)
  sourceDirectories = files(allprojects.sourceSets.main.allSource.srcDirs)
  classDirectories = files(allprojects.sourceSets.main.output)
  executionData = files(allprojects.jacocoTestReport.executionData)
  reports {
    html.enabled = true
    xml.enabled = false
    csv.enabled = false
  }
  onlyIf = {
    true
  }
  doFirst {
    executionData = files(executionData.findAll {
      it.exists()
    })
  }
  afterEvaluate {
    classDirectories = files(classDirectories.files.collect {
      fileTree(dir: it, exclude: ['sample/**'])
    })
  }
}

def packageName = 'blackstrom-core'

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.25'
  compile 'com.obsidiandynamics.yconf:yconf-core:0.2.1'
  compile 'org.apache.commons:commons-lang3:3.7'
}

project.ext.set('benchmarkClasspath', new org.gradle.api.internal.file.UnionFileCollection())
project.ext.set('benchmarkClasses', new ArrayList())

task addBenchmarks() {
  project(':').benchmarkClasspath += sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
  project(':').benchmarkClasses += 'com.obsidiandynamics.blackstrom.bank.MultiNodeQueueRandomBankTransferTest'
  project(':').benchmarkClasses += 'com.obsidiandynamics.blackstrom.ledger.MultiNodeQueueLedgerTest'
}

task launch() {
  dependsOn allprojects*.tasks*.findByName('testClasses')
  doLast {
    def baseJvmArgs = '-XX:-MaxFDLimit -XX:+TieredCompilation -XX:+UseNUMA -XX:+UseCondCardMark -XX:-UseBiasedLocking ' + 
                      '-Xms2G -Xmx2G -Xss1M -XX:+UseParallelGC -Dlauncher.packageCompressLevel=3'
    javaexec {
      systemProperties = System.properties
      classpath = project(':').benchmarkClasspath
      main = 'com.obsidiandynamics.blackstrom.util.Launcher'
      args = project(':').benchmarkClasses
      jvmArgs = Arrays.asList baseJvmArgs.split(' ')
      standardInput = System.in
    }
  }
}

jar {
  baseName packageName
  finalizedBy jacocoRootReport
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = 'javadoc'
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = 'sources'
}
 
artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project(':').group
      artifactId packageName
      version project(':').version
      from components.java
      artifact sourcesJar
      artifact javadocJar
    }
  }
  repositories {
    maven {
      url "${project(':').projectDir}/../repo"
    }
  }
}

def packageDesc = 'Implementation of the Abstract Voting Machine atomic commit protocol'
def repoName = 'blackstrom'
bintray {
  user = System.getenv(envUser)
  key = System.getenv(envKey)
  publications = ['mavenJava']
  pkg {
    repo = "${repoName}"
    name = packageName
    userOrg = 'obsidiandynamics'
    desc = packageDesc
    websiteUrl = "https://github.com/obsidiandynamics/${repoName}"
    licenses = ['BSD New']
    vcsUrl = "https://github.com/obsidiandynamics/${repoName}"
    issueTrackerUrl = "https://github.com/obsidiandynamics/${repoName}/issues"
    publicDownloadNumbers = true
    githubRepo = "obsidiandynamics/${repoName}"
    override = true
    publish = false
    version {
      name = project(':').version
      desc = packageDesc
      released  = new Date()
      vcsTag = project(':').version
    }
  }
}
