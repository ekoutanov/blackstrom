def packageName = "blackstrom-hazelcast"
version = project(":").version

dependencies {
  compile project(":")
  compile "com.hazelcast:hazelcast:3.10-BETA-2"
  
  testCompile project(":blackstrom-codec-kryo")
  testCompile project(":blackstrom-codec-kryo").sourceSets.test.output
  testCompile project(":blackstrom-sim")
  testCompile project(":blackstrom-sim").sourceSets.test.output
  testCompile "com.hazelcast:hazelcast:3.10-BETA-2:tests"
}

task addLauncherApps() {
  project(":").launcherClasspath += sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
  project(":").launcherClasses += "com.obsidiandynamics.blackstrom.bank.HazelQKryoRandomBankTransferTest"
  project(":").launcherClasses += "com.obsidiandynamics.blackstrom.ledger.HazelQKryoLedgerTest"
  project(":").launcherClasses += "com.obsidiandynamics.blackstrom.hazelcast.queue.PubSubOneWayTest"
  project(":").launcherClasses += "com.obsidiandynamics.blackstrom.hazelcast.queue.PubSubRoundTripTest"
}

jar {
  baseName packageName
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = "javadoc"
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = "sources"
}
 
artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project(":").group
      artifactId packageName
      version project(":").version
      from components.java
      artifact sourcesJar
      artifact javadocJar
    }
  }
  repositories {
    maven {
      url "${project(':').projectDir}/../repo"
    }
  }
}

def envUser = "BINTRAY_USER"
def envKey = "BINTRAY_KEY"
def packageDesc = "Hazelcast ledger"
def repoName = "blackstrom"
bintray {
  user = System.getenv(envUser)
  key = System.getenv(envKey)
  publications = ["mavenJava"]
  pkg {
    repo = "${repoName}"
    name = packageName
    userOrg = "obsidiandynamics"
    desc = packageDesc
    websiteUrl = "https://github.com/obsidiandynamics/${repoName}"
    licenses = ["BSD New"]
    vcsUrl = "https://github.com/obsidiandynamics/${repoName}"
    issueTrackerUrl = "https://github.com/obsidiandynamics/${repoName}/issues"
    publicDownloadNumbers = true
    githubRepo = "obsidiandynamics/${repoName}"
    override = true
    publish = false
    version {
      name = project(":").version
      desc = packageDesc
      released  = new Date()
      vcsTag = project(":").version
    }
  }
}

task testJar(type: Jar) {
  dependsOn configurations.compile
  dependsOn configurations.runtime
  dependsOn configurations.testCompile
  dependsOn configurations.testRuntime
  dependsOn jar
  from sourceSets.main.output
  from sourceSets.test.output
  from { // bundle all dependencies
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.testCompile.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.testRuntime.collect { it.isDirectory() ? it : zipTree(it) }
  }   
  baseName packageName + "-test"
}